# namei command for Mac OS
# Usage: namei {path}

if [[ $(uname) != Darwin ]] && (( $+commands[namei] ))
then
   command namei $@
else
   if [[ $(uname) != Darwin ]] && (( ! $+commands[namei] ))
   then
      print -P '%F{red}namei not installed. using the namei function...%f'
   fi

   if [[ $1 == (-h|--help) ]]
   then
      echo 'Usage: namei {path}'
      return
   fi

   local o

   # ignore options
   for o in $@
   do
      if [[ $o == -[lmonvxV] || $o == --(long|modes|owners|nosymlinks|vertical|mountpoints|version) ]]
      then
         shift
      else
         break
      fi
   done

   local inode
   local inodes

   if (( $+commands[realpath] ))
   then
      inode=$(realpath -e -- ${1:-.})
   else
      inode=$(readlink -f -- ${1:-.})
   fi
   inodes=(${(s./.)inode})

   [[ -n $inodes ]] || return 1

   local i ii

   if [[ $(uname) == *BSD ]]
   then
      stat -f '%Sp %Su %Sg %SN' /
   else
      stat -c '%A %U %G %n' /
   fi

   for i in $inodes
   do
      ii=$ii/$i

      if [[ $(uname) == *BSD ]]
      then
         stat -n -f '%Sp %Su %Sg' -- $ii
      else
         stat --printf '%A %U %G' -- $ii
      fi

      print -l -- " ${ii##*/}"
   done
fi
