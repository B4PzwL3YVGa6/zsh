# Start (n)vim using defaults
# Usage: vn    [file(s)]
#        vn -v [file(s)] (use vim even when nvim is installed)

# nvim or vim
if (( $+commands[nvim] )) && [[ $1 != -v ]]
then
   ed=nvim
   ini=init.vim
   if (($#))
   then
      # edit files using default settings
      $ed -u NONE $@
      return
   else
      local opts
      typeset -A opts
      opts["$ed no $ini, no plugins"]='nvim -n -u NONE           '
      opts["$ed    $ini, no plugins"]='nvim -n         --noplugin'
   fi
else
   ed=vim
   ini=.vimrc
   if (($# > 1)) || { (($# == 1)) && [[ $1 != -v ]]; }
   then
      $ed -NX -u NONE $@
      return
   else
      local opts
      typeset -A opts
      opts["$ed no $ini, no plugins"]='vim -nNX -u NONE               '
      opts["$ed    $ini, no plugins"]='vim -nNX -u ~/.vimrc --noplugin'
   fi
fi

local l k v
for k v in ${(Qkv)opts}
do
   printf "$((++l))) %s ( %s )\n" $k $v
done

unset choice
while [[ $choice != [12] ]]
do
   read 'choice?# '
   case $choice in
      (1) ${(z)opts["$ed no $ini, no plugins"]} ;;
      (2) ${(z)opts["$ed    $ini, no plugins"]} ;;
      (*) printf '\nInvalid selection! Choose 1 or 2.\n' >&2
   esac
done
