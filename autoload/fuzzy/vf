# Usage:
#   vf [-l] [fuzzy_pattern]

local files

if [[ $1 != -l ]]
then
   # fuzzy pattern supplied
   if (($#))
   then
      files=(${(0)"$(fzf -0 -m -q"$*" --print0 || echo $pipestatus[1])"})
   else
      files=(${(0)"$(fzf -0 -m --print0 || echo $pipestatus[1])"})
   fi
fi

# use locate if nothing found locally
# note: files == 1 alone throws a division by 0 error with paths (because of /)
if [[ $1 == -l ]] || { [[ $files == [0-9] ]] && ((files == 1)) }
then
   if [[ $(uname) == Darwin && $1 != .* && $2 != .* ]]
   then
      locate=mdfind
   else
      locate=locate
   fi
   if [[ $1 == -l && -n $2 ]] || [[ $1 != -l && -n $1 ]]
   then
      if [[ $1 == -l ]]
      then
         files=(${(0)"$($locate -0 / | grep -z -vE '~$' | fzf --read0 -0 -1 -m -q"$*[2,-1]" --print0)"})
      else
         files=(${(0)"$($locate -0 / | grep -z -vE '~$' | fzf --read0 -0 -1 -m -q"$*" --print0)"})
      fi
   else
      files=(${(0)"$($locate -0 / | grep -z -vE '~$' | fzf --read0 -0 -1 -m --print0)"})
   fi

   if [[ -n $files && $1 != -l ]]
   then
      echo 'used locate!'
   fi
fi

if [[ -n $files && $files != [0-9]* ]]
then
   v -- $files
fi
